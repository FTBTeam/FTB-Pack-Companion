plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

def ENV = System.getenv()

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury-fabric:${rootProject.architectury_version}"

    modApi ("dev.ftb.mods:ftb-library:${rootProject.ftb_library_version}") { transitive false }

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    injectAccessWidener = true
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null
}

jar {
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

def ftbURL = "https://maven.ftb.dev/release"
def sapsURL = "https://maven.saps.dev/minecraft"
def mavenVersion = rootProject.version
if (ENV.SNAPSHOT != null && ENV.SNAPSHOT == "true") {
    ftbURL = "https://maven.ftb.dev/snapshot"
    mavenVersion += "-SNAPSHOT"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = rootProject.maven_group
            artifactId = "${rootProject.archivesBaseName}-${project.name}"
            version = mavenVersion

            artifact(remapJar) {
                builtBy remapJar
            }
        }
    }

    repositories {
        if (ENV.FTB_MAVEN_TOKEN) {
            maven {
                url ftbURL
                credentials {
                    username = "ftb"
                    password = "${ENV.FTB_MAVEN_TOKEN}"
                }
            }
        }

        if (ENV.SAPS_TOKEN) {
            maven {
                url sapsURL
                credentials {
                    username = "ftb"
                    password = "${ENV.SAPS_TOKEN}"
                }
            }
        }
    }
}

def relType = "release"
if (ENV.TAG != null) {
    if (ENV.TAG.contains("alpha")) {
        relType = "alpha"
    } else if (ENV.TAG.contains("beta")) {
        relType = "beta"
    }
}

if (ENV.CURSEFORGE_KEY) {
    curseforge {
        apiKey = ENV.CURSEFORGE_KEY
        project {
            id = rootProject.curseforge_id
            releaseType = relType
            addGameVersion "Fabric"
            addGameVersion rootProject.minecraft_version
            mainArtifact(remapJar.archiveFile)
            relations {
                requiredDependency 'ftb-library-fabric'
            }
            changelog = file("./CHANGELOG.md")
            changelogType = 'markdown'
        }
    }
}
