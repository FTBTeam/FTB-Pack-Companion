architectury {
    common(rootProject.enabled_platforms.split(","))
}

loom {
    accessWidenerPath = file("src/main/resources/ftbpc.accesswidener")
}

dependencies {
    // We depend on fabric loader here to use the fabric @Environment annotations and get the mixin dependencies
    // Do NOT use other classes from fabric loader
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury:${rootProject.architectury_version}"

    modApi ("dev.ftb.mods:ftb-library:${rootProject.ftb_library_version}") { transitive false }
}

def ENV = System.getenv()


def ftbURL = "https://maven.ftb.dev/releases"
def sapsURL = "https://maven.saps.dev/releases"
def mavenVersion = rootProject.version
if (ENV.SNAPSHOT != null && ENV.SNAPSHOT == "true") {
    ftbURL = "https://maven.ftb.dev/releases"
    mavenVersion += "-SNAPSHOT"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = rootProject.maven_group
            artifactId = project.archivesBaseName
            version = mavenVersion

            artifact(remapJar) {
                builtBy remapJar
            }
        }
    }

    repositories {
        if (ENV.FTB_MAVEN_TOKEN) {
            maven {
                url ftbURL
                credentials {
                    username = "ftb"
                    password = "${ENV.FTB_MAVEN_TOKEN}"
                }
            }
        }

        if (ENV.SAPS_TOKEN) {
            maven {
                url sapsURL
                credentials {
                    username = "ftb"
                    password = "${ENV.SAPS_TOKEN}"
                }
            }
        }
    }
}

